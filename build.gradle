buildscript {
	ext {
		springBootVersion = '2.0.3.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}", 
		'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.5')
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    integrationTest { dirName = 'integration-test' }
    functionalTest { dirName = 'functional-test' }
}

group = 'ceiba'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('joda-time:joda-time:2.10')
	compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.+'
	
	runtime('org.springframework.boot:spring-boot-devtools')
	//runtime('mysql:mysql-connector-java')
	runtime('com.h2database:h2')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.mockito:mockito-all:1.10.19')
	integrationTestCompile('org.assertj:assertj-core:3.0.0')
}

test{
	jacoco{
		append=false
		destinationFile =file("build/jacoco/jacocoTest.exec")
	}
	reports.junitXml.destination=file("build/jacoco/test-results")
}

integrationTest{
	jacoco{
		append=false
		destinationFile =file("build/jacoco/jacocoTest.exec")
	}
	reports.junitXml.destination=file("build/jacoco/test-results")
}

functionalTest{
	jacoco{
		append=false
		destinationFile =file("build/jacoco/jacocoTest.exec")
	}
	reports.junitXml.destination=file("build/jacoco/test-results")
}

def webappDir = "$projectDir/src/main/ADNCeiba-Front"
sourceSets {
	main {
		resources {
			srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
		}
	}
}

processResources {
	dependsOn "buildAngular"
}

task buildAngular(type:Exec) {
	// installAngular should be run prior to this task
	dependsOn "installAngular"
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	// Add task to the standard build group
	group = BasePlugin.BUILD_GROUP
	// ng doesn't exist as a file in windows -> ng.cmd
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
		commandLine "ng.cmd", "build"
	} else {
		commandLine "ng", "build"
	}
}
 
task installAngular(type:Exec) {
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
		commandLine "npm.cmd", "install"
	} else {
		commandLine "npm", "install"
	}
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

check.dependsOn functionalTest
functionalTest.mustRunAfter integrationTest
